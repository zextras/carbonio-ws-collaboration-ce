pkgname="carbonio-ws-collaboration"
pkgver="1.2.0"
pkgrel="1"
pkgdesc="Workstream Collaboration for Carbonio"
maintainer="Zextras <packages@zextras.com>"
arch=('x86_64')
license=("AGPL-3.0-only")
section="admin"
priority="optional"
url="https://www.zextras.com/"
depends=(
  "carbonio-openjdk"
  "jq"
  "pending-setups"
  "service-discover"
)
backup=(
  "etc/carbonio/ws-collaboration/logback.xml"
  "etc/zextras/service-discover/carbonio-ws-collaboration.hcl"
)
source=(
  "carbonio-ws-collaboration-configs"
  "carbonio-ws-collaboration-meeting-cleanup"
  "carbonio-ws-collaboration-setup.sh"
  "carbonio-ws-collaboration-sidecar.service"
  "carbonio-ws-collaboration.hcl"
  "carbonio-ws-collaboration.service"
  "carbonio-ws-collaboration"
  "intentions.json"
  "logback.xml"
  "policies.json"
  "service-protocol.json"
)
sha256sums=('5eb685acf4a3542072313e7b7a4cb546f8a650d868457d929d2adb2a6520dc1d'
  '2b87e3164efe958c431a67ae0430e3c1eebc10c896de05d91df6b424df338e5b'
  '97256a878d94c5239c422049c9360ca18e1c34c74b3e6ac2a93e51ae0419b423'
  '1320c458d4daa43b2b11cd694d472b635c2de547a67531605cbc2ac2afbd8b9a'
  '3eb0ea7c2b205c3488aaae074f61b1e0336506f00a676abb787fdd49a11cf5c1'
  '9b083698d7bece4672bfa76dda4a5c0761606c969c3e39a524fbcb82ff39e1bb'
  'ce7262f2c01e4f6c700265f88892d7572dc546d2ff328506dfd7f5502c6b6b0b'
  '58c69efd45348762b3c603ecb8a7d3df5e4ada730b00a760d02d3f376a455d08'
  'a993f63dbfa8273d92f004c6db2886f17d9876d36c65ab757456ab99c0a3f3c3'
  'c40536070ee22157014e3a7d09c8da9c4f7fe5941dd3661457f2e38a8c095e88'
  'd3741e4103a786c40a319248258ee5a25a39174a6ba80973b5877bb8a5e2bf72')

package() {
  cd "${srcdir}"/../../ws-collaboration
  install -Dm 644 ./carbonio-ws-collaboration-boot/target/carbonio-ws-collaboration-fatjar.jar \
    "${pkgdir}/usr/bin/carbonio/ws-collaboration/carbonio-ws-collaboration.jar"

  cd "${srcdir}"
  install -Dm 755 carbonio-ws-collaboration "${pkgdir}/usr/bin/carbonio-ws-collaboration"
  install -Dm 755 carbonio-ws-collaboration-configs "${pkgdir}/usr/bin/carbonio-ws-collaboration-configs"
  install -Dm 755 carbonio-ws-collaboration-meeting-cleanup "${pkgdir}/usr/bin/carbonio-ws-collaboration-meeting-cleanup"
  install -Dm 644 logback.xml "${pkgdir}/etc/carbonio/ws-collaboration/logback.xml"
  install -Dm 644 carbonio-ws-collaboration.service "${pkgdir}/lib/systemd/system/carbonio-ws-collaboration.service"
  install -Dm 644 carbonio-ws-collaboration-sidecar.service "${pkgdir}/lib/systemd/system/carbonio-ws-collaboration-sidecar.service"
  install -Dm 644 carbonio-ws-collaboration.hcl "${pkgdir}/etc/zextras/service-discover/carbonio-ws-collaboration.hcl"
  install -Dm 644 carbonio-ws-collaboration-setup.sh "${pkgdir}/etc/zextras/pending-setups.d/carbonio-ws-collaboration.sh"
  install -Dm 644 policies.json "${pkgdir}/etc/carbonio/ws-collaboration/service-discover/policies.json"
  install -Dm 644 intentions.json "${pkgdir}/etc/carbonio/ws-collaboration/service-discover/intentions.json"
  install -Dm 644 service-protocol.json "${pkgdir}/etc/carbonio/ws-collaboration/service-discover/service-protocol.json"
}

postinst() {
  getent group 'carbonio-ws-collaboration' >/dev/null ||
    groupadd -r 'carbonio-ws-collaboration'
  getent passwd 'carbonio-ws-collaboration' >/dev/null ||
    useradd -r -M -g 'carbonio-ws-collaboration' -s /sbin/nologin 'carbonio-ws-collaboration'

  mkdir -p /var/log/carbonio/ws-collaboration
  chown carbonio-ws-collaboration:carbonio-ws-collaboration /var/log/carbonio/ws-collaboration

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-ws-collaboration.service >/dev/null 2>&1 || :
    systemctl enable carbonio-ws-collaboration-sidecar.service >/dev/null 2>&1 || :
  fi

  echo "========================================================="
  echo "Carbonio Workstream Collaboration installed successfully!"
  echo "You must run pending-setups to configure it correctly.   "
  echo "========================================================="
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-ws-collaboration.service >/dev/null 2>&1 || :
    systemctl --no-reload disable carbonio-ws-collaboration-sidecar.service >/dev/null 2>&1 || :
    systemctl stop carbonio-ws-collaboration.service >/dev/null 2>&1 || :
    systemctl stop carbonio-ws-collaboration-sidecar.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  rm -f /etc/carbonio/ws-collaboration/service-discover/token
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}

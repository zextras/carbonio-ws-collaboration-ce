pkgname="carbonio-ws-collaboration"
pkgver="1.4.0"
pkgrel="1"
pkgdesc="Workstream Collaboration for Carbonio"
maintainer="Zextras <packages@zextras.com>"
arch=('x86_64')
license=("AGPL-3.0-only")
section="admin"
priority="optional"
url="https://www.zextras.com/"
depends=(
  "carbonio-openjdk"
  "jq"
  "pending-setups"
  "service-discover"
)
backup=(
  "etc/carbonio/ws-collaboration/logback.xml"
  "etc/zextras/service-discover/carbonio-ws-collaboration.hcl"
)
source=(
  "carbonio-ws-collaboration"
  "carbonio-ws-collaboration-configs"
  "carbonio-ws-collaboration-setup"
  "carbonio-ws-collaboration-pending-setup"
  "carbonio-ws-collaboration.hcl"
  "carbonio-ws-collaboration.service"
  "carbonio-ws-collaboration-sidecar.service"
  "logback.xml"
  "intentions.json"
  "policies.json"
  "service-protocol.json"
)
sha256sums=('7233d077c9cda00b53f482c30cc86af8ca37362f43f6c7f164d7d28c73447c3e'
  'a826c07e9d178928ce73fd13558f69aa7a6e4fd031d915349248a8bb0485c0f0'
  '3b7439a5c9e2c84af0508ac9b4e20ca904dbf276a129c4c3c65f1fcb31ac9757'
  'e9f6604fd073a5cf3cf9b448d6f9f372a64e23f84fc26d7a0fac6d55b11f0c6a'
  'eeb6df23b65ce9964f6d25f9737917eb2cf50ff8ee01cc0053f9510b926b018b'
  '37b6593426aa0e5a209def34a25fee30907e40278b9f35cc6e21b62303b7c39a'
  '1320c458d4daa43b2b11cd694d472b635c2de547a67531605cbc2ac2afbd8b9a'
  'a993f63dbfa8273d92f004c6db2886f17d9876d36c65ab757456ab99c0a3f3c3'
  '58c69efd45348762b3c603ecb8a7d3df5e4ada730b00a760d02d3f376a455d08'
  'c40536070ee22157014e3a7d09c8da9c4f7fe5941dd3661457f2e38a8c095e88'
  'ef55ad102e9a356478233654a104fb84534170c30f765371cd91fb32c3be3578')

package() {
  cd "${srcdir}"/../../ws-collaboration
  install -Dm 755 ./carbonio-ws-collaboration-boot/target/carbonio-ws-collaboration-fatjar.jar \
    "${pkgdir}/usr/share/carbonio/carbonio-ws-collaboration.jar"

  cd "${srcdir}"
  install -Dm 755 carbonio-ws-collaboration "${pkgdir}/usr/bin/carbonio-ws-collaboration"
  install -Dm 755 carbonio-ws-collaboration-configs "${pkgdir}/usr/bin/carbonio-ws-collaboration-configs"
  install -Dm 755 carbonio-ws-collaboration-setup "${pkgdir}/usr/bin/carbonio-ws-collaboration-setup"
  install -Dm 644 carbonio-ws-collaboration-pending-setup "${pkgdir}/etc/zextras/pending-setups.d/carbonio-ws-collaboration.sh"
  install -Dm 644 carbonio-ws-collaboration.hcl "${pkgdir}/etc/zextras/service-discover/carbonio-ws-collaboration.hcl"
  install -Dm 644 carbonio-ws-collaboration.service "${pkgdir}/lib/systemd/system/carbonio-ws-collaboration.service"
  install -Dm 644 carbonio-ws-collaboration-sidecar.service "${pkgdir}/lib/systemd/system/carbonio-ws-collaboration-sidecar.service"
  install -Dm 644 logback.xml "${pkgdir}/etc/carbonio/ws-collaboration/logback.xml"
  install -Dm 644 intentions.json "${pkgdir}/etc/carbonio/ws-collaboration/service-discover/intentions.json"
  install -Dm 644 policies.json "${pkgdir}/etc/carbonio/ws-collaboration/service-discover/policies.json"
  install -Dm 644 service-protocol.json "${pkgdir}/etc/carbonio/ws-collaboration/service-discover/service-protocol.json"
}

postinst() {
  getent group 'carbonio-ws-collaboration' >/dev/null ||
    groupadd -r 'carbonio-ws-collaboration'
  getent passwd 'carbonio-ws-collaboration' >/dev/null ||
    useradd -r -M -g 'carbonio-ws-collaboration' -s /sbin/nologin 'carbonio-ws-collaboration'

  mkdir -p /var/log/carbonio/ws-collaboration
  chown carbonio-ws-collaboration:carbonio-ws-collaboration /var/log/carbonio/ws-collaboration

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-ws-collaboration.service >/dev/null 2>&1 || :
    systemctl enable carbonio-ws-collaboration-sidecar.service >/dev/null 2>&1 || :
  fi

  echo "========================================================="
  echo "Carbonio Workstream Collaboration installed successfully!"
  echo "You must run pending-setups to configure it correctly.   "
  echo "========================================================="
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-ws-collaboration.service >/dev/null 2>&1 || :
    systemctl --no-reload disable carbonio-ws-collaboration-sidecar.service >/dev/null 2>&1 || :
    systemctl stop carbonio-ws-collaboration.service >/dev/null 2>&1 || :
    systemctl stop carbonio-ws-collaboration-sidecar.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  rm -f /etc/carbonio/ws-collaboration/service-discover/token
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}

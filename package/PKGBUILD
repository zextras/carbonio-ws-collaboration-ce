pkgname="carbonio-ws-collaboration-ce"
pkgver="1.6.2"
pkgrel="2"
pkgdesc="Workstream Collaboration for Carbonio CE"
maintainer="Zextras <packages@zextras.com>"
arch=('x86_64')
license=("AGPL-3.0-only")
section="admin"
priority="optional"
url="https://www.zextras.com/"
depends=(
  "carbonio-openjdk"
  "jq"
  "pending-setups"
  "service-discover"
)
backup=(
  "etc/carbonio/ws-collaboration/logback.xml"
  "etc/zextras/service-discover/carbonio-ws-collaboration.hcl"
)
source=(
  "carbonio-ws-collaboration"
  "carbonio-ws-collaboration-ce-fatjar.jar"
  "carbonio-ws-collaboration-configs"
  "carbonio-ws-collaboration-setup"
  "carbonio-ws-collaboration-pending-setup"
  "carbonio-ws-collaboration.hcl"
  "carbonio-ws-collaboration.service"
  "carbonio-ws-collaboration-sidecar.service"
  "logback.xml"
  "intentions.json"
  "policies.json"
  "service-protocol.json"
)
sha256sums=(
  '7233d077c9cda00b53f482c30cc86af8ca37362f43f6c7f164d7d28c73447c3e'
  'SKIP'
  '159c495e8f974ca856a177071d6bf373c090994d50745d8ffaa51bd58678d2d3'
  '3b7439a5c9e2c84af0508ac9b4e20ca904dbf276a129c4c3c65f1fcb31ac9757'
  'e9f6604fd073a5cf3cf9b448d6f9f372a64e23f84fc26d7a0fac6d55b11f0c6a'
  '97dafb135eec3ee429ee3c0044e8f5e5ad368314c43a32dc7a871b1efd114fa1'
  'd119cef22e0de99b2105cc87f479aa3ddfec3d51cf709353810267e61c430ada'
  '99b886122ee345aed5ed9b807dabf4b86c645e99c8040ec70a0c0a80fb016efe'
  '99fcaf768357c8be4a4301a1edf63d7059f5c032cf38a889cd3114d4737f60ee'
  '58c69efd45348762b3c603ecb8a7d3df5e4ada730b00a760d02d3f376a455d08'
  '7dbc2225d51fb07c1e3e5577f8ba80d8cd9634c7c47f5b0ab1edbc949d97ff21'
  'a805fcb27d94d7d935fed0e335850137d38e74b43dab06f478acf3308c25f089'
)

package() {
  cd "${srcdir}"
  install -Dm755 carbonio-ws-collaboration-ce-fatjar.jar \
    "${pkgdir}/usr/share/carbonio/carbonio-ws-collaboration.jar"
  install -Dm755 carbonio-ws-collaboration \
    "${pkgdir}/usr/bin/carbonio-ws-collaboration"
  install -Dm755 carbonio-ws-collaboration-configs \
    "${pkgdir}/usr/bin/carbonio-ws-collaboration-configs"
  install -Dm755 carbonio-ws-collaboration-setup \
    "${pkgdir}/usr/bin/carbonio-ws-collaboration-setup"
  install -Dm644 carbonio-ws-collaboration-pending-setup \
    "${pkgdir}/etc/zextras/pending-setups.d/carbonio-ws-collaboration.sh"
  install -Dm644 carbonio-ws-collaboration.hcl \
    "${pkgdir}/etc/zextras/service-discover/carbonio-ws-collaboration.hcl"
  install -Dm644 carbonio-ws-collaboration.service \
    "${pkgdir}/lib/systemd/system/carbonio-ws-collaboration.service"
  install -Dm644 carbonio-ws-collaboration-sidecar.service \
    "${pkgdir}/lib/systemd/system/carbonio-ws-collaboration-sidecar.service"
  install -Dm644 logback.xml \
    "${pkgdir}/etc/carbonio/ws-collaboration/logback.xml"
  install -Dm644 intentions.json \
    "${pkgdir}/etc/carbonio/ws-collaboration/service-discover/intentions.json"
  install -Dm644 policies.json \
    "${pkgdir}/etc/carbonio/ws-collaboration/service-discover/policies.json"
  install -Dm644 service-protocol.json \
    "${pkgdir}/etc/carbonio/ws-collaboration/service-discover/service-protocol.json"
}

postinst() {
  getent group 'carbonio-ws-collaboration' >/dev/null ||
    groupadd -r 'carbonio-ws-collaboration'
  getent passwd 'carbonio-ws-collaboration' >/dev/null ||
    useradd -r -M -g 'carbonio-ws-collaboration' -s /sbin/nologin 'carbonio-ws-collaboration'

  mkdir -p /var/log/carbonio/ws-collaboration
  chown carbonio-ws-collaboration:carbonio-ws-collaboration /var/log/carbonio/ws-collaboration

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-ws-collaboration.service >/dev/null 2>&1 || :
    systemctl enable carbonio-ws-collaboration-sidecar.service >/dev/null 2>&1 || :
  fi

  echo "========================================================="
  echo "Carbonio Workstream Collaboration installed successfully!"
  echo "You must run pending-setups to configure it correctly.   "
  echo "========================================================="
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-ws-collaboration.service >/dev/null 2>&1 || :
    systemctl --no-reload disable carbonio-ws-collaboration-sidecar.service >/dev/null 2>&1 || :
    systemctl stop carbonio-ws-collaboration.service >/dev/null 2>&1 || :
    systemctl stop carbonio-ws-collaboration-sidecar.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  rm -f /etc/carbonio/ws-collaboration/service-discover/token
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
